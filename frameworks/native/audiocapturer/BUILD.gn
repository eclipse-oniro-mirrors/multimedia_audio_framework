# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("../../../audio_ohcore.gni")
import("../../../config.gni")

pulseaudio_dir = "//third_party/pulseaudio"

config("audio_capturer_config") {
  include_dirs = [
    "include",
    "../../../interfaces/inner_api/native/audiocommon/include",
    "../../../interfaces/inner_api/native/audiocapturer/include",
    "../../../frameworks/native/audiostream/include",
    "../../../services/audio_service/client/include",
    "$pulseaudio_dir/src",
    "$pulseaudio_dir/confgure/src",
  ]

  cflags = [
    "-Wall",
    "-Werror",
  ]
}

ohos_shared_library("audio_capturer") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
    blocklist = "../../../cfi_blocklist.txt"
  }
  install_enable = true

  configs = [ ":audio_capturer_config" ]

  include_dirs = audio_gateway_include_dirs

  include_dirs += [ multimedia_audio_framework_pulse_audio ]

  sources = [
    "src/audio_capturer.cpp",
    "src/audio_capturer_proxy_obj.cpp",
  ]

  deps = [
    "../../../services/audio_policy:audio_policy_client",
    "../../../services/audio_service:audio_client",
  ]

  deps += audio_capturer_gateway_deps

  public_configs = [ ":audio_external_library_config" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_single",
  ]

  version_script = "../../../audio_framework.versionscript"
  innerapi_tags = [ "platformsdk" ]

  part_name = "audio_framework"
  subsystem_name = "multimedia"
}

config("audio_external_library_config") {
  include_dirs =
      [ "../../../interfaces/inner_api/native/audiocapturer/include" ]
}

group("audio_capturer_test_packages") {
  deps = [ ":audio_capturer_test" ]
}

ohos_executable("audio_capturer_test") {
  install_enable = false

  sources = [ "test/example/audio_capturer_test.cpp" ]

  configs = [ ":audio_capturer_config" ]

  deps = [ ":audio_capturer" ]

  external_deps = [ "hilog:libhilog" ]

  part_name = "audio_framework"
  subsystem_name = "multimedia"
}
